/*
 * Copyright (c) 2020 D4L data4life gGmbH / All rights reserved.
 *
 * D4L owns all legal rights, title and interest in and to the Software Development Kit ("SDK"),
 * including any intellectual property rights that subsist in the SDK.
 *
 * The SDK and its documentation may be accessed and used for viewing/review purposes only.
 * Any usage of the SDK for other purposes, including usage for the development of
 * applications/third-party applications shall require the conclusion of a license agreement
 * between you and D4L.
 *
 * If you are interested in licensing the SDK for your own applications/third-party
 * applications and/or if youâ€™d like to contribute to the development of the SDK, please
 * contact D4L by email to help@data4life.care.
 */

apply plugin: 'maven-publish'

group = LibraryConfig.group

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task androidJavadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    options.linkSource true
    options.addStringOption('encoding', 'UTF-8')
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task androidJavadocJar(type: Jar, dependsOn: androidJavadoc) {
    classifier = 'javadoc'
    from androidJavadoc.destinationDir
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocJar
}

androidJavadoc {
    failOnError false
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GithubPackages"
                url = uri("https://maven.pkg.github.com/d4l-data4life/hc-sdk-kmp")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("PACKAGE_REGISTRY_USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("PACKAGE_REGISTRY_TOKEN")
                }
            }

            def target = "file://${project.rootProject.buildDir}/gitPublish"

            maven {
                name = "ReleasePackages"
                url = uri("$target/maven-releases/releases")
            }

            maven {
                name = "SnapshotPackages"
                url = uri("$target/maven-snapshots/snapshots")
            }

            maven {
                name = "FeaturePackages"
                url = uri("$target/maven-features/features")
            }
        }
        publications {
            maven(MavenPublication) {
                groupId = "${LibraryConfig.publish.groupId}.${LibraryConfig.publish.name}"

                from(components["release"])
                artifact(tasks["androidSourcesJar"])
                pom {
                    name.set(LibraryConfig.publish.name)
                    description.set(LibraryConfig.publish.description)
                    url.set(LibraryConfig.publish.url)
                    inceptionYear.set(LibraryConfig.publish.year)

                    scm {
                        connection.set(LibraryConfig.publish.scmConnection)
                        developerConnection.set(LibraryConfig.publish.scmDeveloperConnection)
                        url.set(LibraryConfig.publish.scmUrl)
                    }
                    licenses {
                        license {
                            name.set("${LibraryConfig.publish.licenseName}")
                            url.set("${LibraryConfig.publish.licenseUrl}")
                            distribution.set("${LibraryConfig.publish.licenseDistribution}")
                        }
                    }
                    developers {
                        developer {
                            id.set("${LibraryConfig.publish.developerId}")
                            name.set("${LibraryConfig.publish.developerName}")
                            email.set("${LibraryConfig.publish.developerEmail}")
                        }
                    }
                }
            }
        }
    }
}
